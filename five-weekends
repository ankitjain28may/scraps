#!/usr/bin/perl

# Debunking the five weekends myth
# http://www.snopes.com/inboxer/trivia/fivedays.asp

use strict;
use warnings;

use feature 'say';

use DateTime;

my $start = 2013;
my $end   = $start + 823 - 1;
say "Months with five full weekends between $start and $end";

my $nmonths = 0;
for my $year ( $start .. $end ) {
    for my $month ( 1..12 ) {
        my $eom = DateTime->last_day_of_month( year => $year, month => $month );
        if ( $eom->day == 31 && $eom->day_of_week == 7 ) {
            say $eom->month_name, ' ', $year;
            ++$nmonths;
        }
    }
}
say "There will be $nmonths months with five full weekends in the 823 years between $start and $end.";
